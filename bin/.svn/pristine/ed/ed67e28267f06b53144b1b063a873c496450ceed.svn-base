(:~ 
 : _request_getters.mod.xq - getter functions delivering parameter values
 :
 : Supported parameter item types:
 :
 : xs:integer 
 : xs:negativeInteger
 : xs:nonNegativeInteger
 : xs:nonPositiveInteger
 : xs:positiveInteger
 : xs:string
 :
 : @version 20140325-1 new item type 'directory'
 : @version 20140217-1 first version 
 : ===================================================================================
 :)

module namespace m="http://www.ttools.org/xquery-functions";

import module namespace tt="http://www.ttools.org/xquery-functions" at
   "_stringTools.mod.xq",
   "_constants.mod.xq",
   "_csvParser.mod.xq";   

(:~
 : Returns the operation name.
 : 
 : @param request the request element
 : @return the operation name
 :)
declare function m:getOperationName($request as element()?)
      as item()* {
    if (not($request)) then () else local-name($request)
};

(:~
 : Returns the names of available parameters, optionally filtered by
 : a name filter.
 :
 : @param request the request element
 : @param nameFilter a name filter
 : @return the naes of available parameters, optionally 
 :    filtered by the supplied namefilter
 :)
declare function m:getParamNames($request as element()?, $nameFilter as xs:string?)
        as xs:string* {
    let $allNames := $request/*/local-name(.)
    return
        if (not($nameFilter)) then $allNames 
        else
            let $filter := tt:parseNameFilter($nameFilter)
            return
                tt:filterNames($allNames, $filter)       
};

(:~
 : Returns true if a parameter exsts, false otherwise.
 : 
 : @param request the request element
 : @param name the parameter name 
 : @return boolean value flagging parameter existence
 :)
declare function m:paramExists($request as element()?, $name as xs:string)
      as xs:boolean? {
    if (not($request)) then () else    
        boolean(m:_getParamElem($request, $name)) 
};

(:~
 : Returns a sequence of parameter values. The parameter names
 : can be specified by distinct items and/or as whitespace-separated
 : list of names.
 : 
 : @param request the request element
 : @param names the parameter names 
 : @return the sequence of parameter values
 :)
declare function m:getParams($request as element()?, $names as xs:string+)
      as item()* {
    let $names := 
        if (not(contains($names[1], ' '))) then $names else
            tokenize(normalize-space($names), ' ')
    return            
        if (not($request)) then () else 
            for $name in $names return m:getParam($request, $name)
};

(:~
 : Returns a parameter value.
 : 
 : @param request the request element
 : @param name the parameter name 
 : @return the parameter value
 :)
declare function m:getParam($request as element()?, $name as xs:string)
      as item()* {
    if (not($request)) then () else
    
    let $paramElem := m:_getParamElem($request, $name) 
    return if (not($paramElem)) then () else m:_getParam($paramElem)
    
};

declare function m:_getParam($paramElem as element()) 
        as item()* {
    let $itemType := $paramElem/@itemType/string()
    let $untypedItems := m:_getUntypedParamItems($paramElem)   
    return
        if ($itemType eq 'xs:string') then $untypedItems
        else if ($itemType eq 'xs:NCName') then
            for $item in $untypedItems return xs:NCName($item)
        else if ($itemType eq 'xs:boolean') then
            for $item in $untypedItems return xs:boolean($item)
        else if ($itemType eq 'xs:decimal') then
            for $item in $untypedItems return xs:decimal($item)
        else if ($itemType eq 'xs:float') then
            for $item in $untypedItems return xs:float($item)
        else if ($itemType eq 'xs:double') then
            for $item in $untypedItems return xs:double($item)
        else if ($itemType eq 'xs:int') then
            for $item in $untypedItems return xs:int($item)
        else if ($itemType eq 'xs:integer') then
            for $item in $untypedItems return xs:integer($item)
        else if ($itemType eq 'xs:positiveInteger') then
            for $item in $untypedItems return xs:positiveInteger($item)
        else if ($itemType eq 'xs:nonNegativeInteger') then
            for $item in $untypedItems return xs:nonNegativeInteger($item)
        else if ($itemType eq 'xs:negativeInteger') then
            for $item in $untypedItems return xs:negativeInteger($item)
        else if ($itemType eq 'xs:nonPositiveInteger') then
            for $item in $untypedItems return xs:nonPositiveInteger($item)
        else if ($itemType eq 'xs:date') then
            for $item in $untypedItems return xs:date($item)
        else if ($itemType eq 'nameFilter') then
            for $item in $untypedItems return $item  
        else if (matches($itemType, '^nameFilterMap(\(.*\))?$')) then
            for $item in $untypedItems return $item  
        else if ($itemType eq 'pathFilter') then
            for $item in $untypedItems return $item  
        else if ($itemType eq 'dfd') then
            for $item in $untypedItems return $item  
        else if ($itemType eq 'docURI') then
            for $item in $untypedItems return doc($item)
        else if ($itemType eq 'textURI') then
            for $item in $untypedItems 
            let $itext := replace($item, '\\', '/')
            let $fields := tt:_splitString($itext, '%')
            let $encoding := ($fields[2], 'ISO-8859-1')[1]
            let $uri := $fields[1]
            return
                unparsed-text($uri, $encoding)
        else if ($itemType eq 'textLinesURI') then
            for $item in $untypedItems 
            let $itext := replace($item, '\\', '/')
            let $fields := tt:_splitString($itext, '%')
            let $encoding := ($fields[2], 'ISO-8859-1')[1]
            let $uri := $fields[1]
            return
                unparsed-text-lines($uri, $encoding)
        else if ($itemType eq 'docDFD') then
            for $item in $untypedItems return $item//@href/doc(.)
        else if ($itemType eq 'txtDFD') then
            for $item in $untypedItems
            let $encoding := ($item/@encoding, 'ISO-8859-1')[1]            
            for $uri in $item//@href
            return
                unparsed-text($uri, $encoding)
        else if ($itemType eq 'textLinesDFD') then
            for $item in $untypedItems 
            let $encoding := ($item/@encoding, 'ISO-8859-1')[1]            
            for $uri in $item//@href
            return
                unparsed-text-lines($uri, $encoding)
        else if ($itemType eq 'docCAT') then
            for $item in $untypedItems return tt:resolveDcat(doc($item))
        else if ($itemType eq 'csvURI') then
            for $item in $untypedItems 
            let $itext := replace($item, '\\', '/')
            let $fields := tt:_getItemFields($itext)
            let $uri := $fields[1]            
            let $encoding := ($fields[2], 'ISO-8859-1')[1]
            let $sep := ($fields[3], ';')[1] 
            let $delim := ($fields[4], '"')[1]            
            let $fromRec := 
                let $sval := $fields[5] return 
                    if (empty($sval)) then () else xs:integer($sval)
            let $toRec := 
                let $sval := $fields[6] return 
                    if (empty($sval)) then () else xs:integer($sval)
            return
                tt:parseCsv($uri, $encoding, $sep, $delim, $fromRec, $toRec)
        else if ($itemType eq 'csvDFD') then
            for $item in $untypedItems
            let $encoding := ($item/@encoding, 'ISO-8859-1')[1]           
            for $uri in $item//@href
            let $sep := $item/@sep 
            let $delim := $item/@delim            
            let $fromRec := $item/@fromRec
            let $toRec := $item/@toRec 
            return
                tt:parseCsv($uri, $encoding, $sep, $delim, $fromRec, $toRec)
        else if ($itemType eq 'directory') then         
            for $item in $untypedItems return $item
        else if (tokenize($m:NON_STANDARD_TYPES, ' ') = $itemType) then
            for $item in $untypedItems return $item            
        else error(QName($tt:URI_ERROR, 'UNKNOWN_ITEM_TYPE'), concat('Unknown item type: ', $itemType))
};

(:~
 : Returns the element representing a request parameter.
 : 
 : @param request the request element
 : @param name the parameter name 
 : @return the element representing the parameter value
 :)
declare function m:_getParamElem($request as element()?, $name as xs:string)
      as element()? {
    $request/*[local-name(.) eq $name]
};

declare function m:_getUnparsedParamItems($param as element())
        as item()* {
    if ($param/value) then $param/value/string(.) 
    else if ($param/@sep) then tokenize($param, $param/@sep)
    else $param/string(.)
};

declare function m:_getUntypedParamItems($param as element())
        as item()* {
    let $nodeItems := $param/@nodeItems/xs:boolean(.) return

    if ($param/value) then 
        if ($nodeItems) then $param/value/* else $param/value/string(.) 
    else if ($param/@sep) then tokenize($param, $param/@sep)
    else if ($nodeItems) then $param/* else $param/string(.)
};

(:~
 : Returns the string value of a parameter. If the parameter
 : is stored as separate items, the semicolon concatenated value 
 : is delivered (escaping any literal semicolon characters by a
 : preceding backslash).
 : 
 : @param request the request element
 : @param name the paramter name
 : @return the parameter value, or the default value
 :)
declare function m:getParamStringValue($request as element(), 
                                       $name as xs:string)
    as xs:string? {
    let $elem := m:_getParamElem($request, $name)
    return
        if (not($elem)) then ()
        else if (not($elem/*)) then string($elem) 
        else string-join($elem/*/replace(., ';', '\\;'), ';')
};

