(: errorAssistent.mod.xq - provides utilities for creating and processing errors
 :
 : @version 20140918-1 first version 
 : ===================================================================================
 :)

module namespace m="http://www.ttools.org/xquery-functions";

import module namespace i="http://www.ttools.org/xquery-functions" at
    "_constants.mod.xq";

declare namespace z="http://www.ttools.org/structure";

(:~
 : Creates an error element.
 :
 : @param errorCode the error code
 : @param msg the error message
 : @param details an element whose attributes are copied into the
 :    error element, specifying details, where the name and value
 :    of an attribute specifies the kind and value of a detail
 :)
declare function m:createError($errorCode as xs:string,
                               $msg as xs:string,
                               $details as element()?)
        as element(z:error) {
    <z:error type="{$errorCode}">{
        $details/@*,
        attribute msg {$msg}
    }</z:error> 
};        

(:~
 : Creates an error element reporting a parameter type error. This
 : function is meant for use within the parser function of a user-defined
 : type.
 :
 : @param itemType the item type
 : @param itemText the item text
 : @param a message which will be appended to a standard prefix 
 :   revealing the type name and the parameter text value
 : @return an error element
 :)
declare function m:createTypeError($itemType as xs:string,
                                   $itemText as xs:string,
                                   $msg as xs:string)
        as element(z:error) {
    let $message := concat(
        "Value '", $itemText, "' not a valid instance of type ", $itemType, "; ", 
        lower-case(substring($msg, 1, 1)), substring($msg, 2))
    return        
        <z:error type="PARAMETER_TYPE_ERROR" itemType="{$itemType}" itemText="{$itemText}" msg="{$message}"/> 
};

(:~
 : Creates an error element reporting a parameter facet error. This
 : function is meant for use within the check function of a user-defined
 : facet.
 :
 : @param paramName the parameter name
 : @param facetType the type of the facet which has been violated
 : @param facetValue the value of the facet which has been violated 
 : @param itemValue the item value which violates the facet
 : @param msgSuffix a message which will be appended to a standard prefix 
 :   revealing the parameter name and type and the facet type and value
 : @return an error element
 :)
declare function m:createFacetError($paramName as xs:string,
                                    $facetType as xs:string,
                                    $facetValue as xs:string,
                                    $itemValue as xs:string,
                                    $msgSuffix as xs:string?)
        as element(z:error) {
    let $message := concat(
        "Parameter '", $paramName, "': item value ('", $itemValue, "') not facet-valid; facet=", $facetType,
        ", facet value=", $facetValue, $msgSuffix)
    return        
        <z:error type="PARAMETER_FACET_ERROR" paramName="{$paramName}" itemValue="{$itemValue}" facetType="{$facetType}" 
                 facetValue="{$facetValue}" msg="{$message}"/> 
};


declare function m:createStandardTypeError($paramName as xs:string,
                                           $itemType as xs:string,
                                           $itemValue as xs:string)
        as element(z:error) {
    let $typeName := replace($itemType, '^xs:', '')
    let $message := concat(
        "Parameter '", $paramName, "': item value ('", $itemValue, "') not a valid ", $typeName, " value")
    return        
        <z:error type="PARAMETER_TYPE_ERROR" paramName="{$paramName}" itemType="{$itemType}" itemValue="{$itemValue}" msg="{$message}"/> 
};

declare function m:createStandardTypeError($paramName as xs:string,
                                           $itemType as xs:string,
                                           $itemValue as xs:string,
                                           $msgSuffix as xs:string?)
        as element(z:error) {
    let $typeName := replace($itemType, '^xs:', '')
    let $message := concat(
        "Parameter '", $paramName, "': item value ('", $itemValue, "') not a valid ", $typeName, " value", $msgSuffix)
    return        
        <z:error type="PARAMETER_TYPE_ERROR" paramName="{$paramName}" itemType="{$itemType}" itemValue="{$itemValue}" msg="{$message}"/> 
};

(:~
 : Extracts from a sequence `error` elements and wraps them in an
 : `errors` element. Extracted `error` elements may be top-level
 : or descendants of the input elements. In particular, they may or
 : may not be contained by `z:errors` elements. Returns the empty 
 : sequence if the input sequence does not contain `error` 
 : elements.
 :
 : @param errors the error elements
 : @return the container element containing the error elements
 :) 
declare function m:extractErrors($elems as element()*)
        as element(z:errors)? {
    let $errors := $elems/
        (descendant-or-self::z:error, descendant-or-self::z:errors/*)
    return
        if (not($errors)) then () else
            <z:errors>{$errors}</z:errors>        
};

(:~
 : Wraps a sequence of error elements in a container element.
 : The input sequence $errors is expected to consist of
 : `z:error` and/or `z:errors` elements.
 :
 : @param errors a sequence of `z:error` and/or `z:errors` elements
 : @return the container element containing the error elements
 :) 
declare function m:wrapErrors($errors as element()*)
        as element(z:errors)? {
    let $errorElems := ($errors/self::z:error, $errors/self::z:errors/z:error)
    return
        if (empty($errorElems)) then () else <z:errors>{$errorElems}</z:errors>        
};

(:~
 : Writes an error report. Note that the 'error' elements may be in the
 : general topicTools namespace, as well as in a tool specific namespace.
 : Therefore, the element test does not specify an element name.
 :)
declare function m:_getErrorReport($errors as element()*, $format as xs:string?)
        as element(z:errorReport) {
    m:_getErrorReport($errors, 'Invalid call', 'type', $format)
};        

(:~
 : Writes an error report. Note that the 'error' elements may be in the
 : general topicTools namespace, as well as in a tool specific namespace.
 : Therefore, the element test does not specify an element name.
 :)
declare function m:_getErrorReport($errors as element()*, $label as xs:string, $leftColAtt as xs:string, $format as xs:string?)
        as element(z:errorReport) {
    let $errors := for $err in $errors return if ($err/self::z:errors) then $err/* else $err        
    let $msgs := $errors/concat(m:_padRight(@*[local-name(.) eq $leftColAtt], 40), m:_foldText(@msg, 100, 40, 43))
    return
    <z:errorReport>{
        string-join((
            '', 
            $label,
            string-join(for $i in 1 to string-length($label) return '=', ''),
            '',
            $msgs, 
            '',
            '-------------------------------------',
            ''
        ), '&#xA;')  
    }</z:errorReport>
};        

