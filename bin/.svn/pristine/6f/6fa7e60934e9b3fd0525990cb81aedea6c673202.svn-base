(: logOperations.mod.xq - reports the service operations observed in a JMeter log
 :
 : @version 20140124-1 
 : ===================================================================================
 :)

(:~@operations
    <operations>
        <operation name="add" func="addModule" type="empty-sequence()">
            <param name="dir" type="directory"/>
            <param name="name" type="xs:NCName?"/>          
            <param name="mod" type="xs:NCName"/>
            <param name="ops" type="xs:NCName+"/>            
            <param name="flavor" type="xs:string*" values="xq10, xq10f, xq30, xq30f" default="xq30f"/>           
        </operation>
    </operations>   
:)

module namespace f="http://www.ttools.org/ttools/xquery-functions";
import module namespace tt="http://www.ttools.org/xquery-functions" at
    "_constants.mod.xq",
    "_request.mod.xq",
    "_reportAssistent.mod.xq",
    "_nameFilter.mod.xq";
    
import module namespace i="http://www.ttools.org/ttools/xquery-functions" at    
    "util.mod.xq";

declare namespace z="http://www.ttools.org/ttools/structure";
declare namespace soap="http://schemas.xmlsoap.org/soap/envelope/";

(:~
 : Installs or updates a topic tool application.
 :
 : @param request the operation request
 : @return no return value, side effect is the installation / an update
 :    of the topic tool application.
 :) 
declare function f:addModule($request as element())
        as node()* {
    let $dir as xs:string := tt:getParam($request, 'dir')    
    let $useDir := resolve-uri($dir, static-base-uri())
    let $mod as xs:NCName := tt:getParam($request, 'mod')
    let $useMod := concat($useDir, '/', $mod)
    return
        (: check that module does not already exist :)
        if (file:exists($useMod)) then
            <z:error type="INVALID_PARAMETER_VALUE" paramName="mod" itemValue="{$mod}" 
                msg="{concat('Module ''', $mod, ''' already exists')}"/> 
        
        else
        
    let $name as xs:NCName := 
        let $nameParam as xs:NCName? := tt:getParam($request, 'name')
        return i:getToolName($dir, $nameParam)         
    let $settings := i:getToolSettings($dir, $name)    
    let $flavor as xs:string := trace( i:getToolFlavor($request, $settings) , 'FLAVOR: ') 
    let $namespace as xs:string := i:getToolNamespace($request, $name, $settings)                    
    let $features as xs:string* := f:featuresFromFlavor($flavor)
        


    let $ops as xs:string+ := tt:getParam($request, 'ops')       
    let $moduleText := f:writeModule($name, $namespace, $mod, $ops, $features)
    
    let $ttoolsUri := replace(replace(static-base-uri(), '^file:/+(.:)?', ''), '^(.*)/.*', '$1/ttools.xq')    
    let $moduleFname := concat($mod, '.mod.xq')    
    let $toolUri as xs:string := 
        let $raw := concat($dir, $name, '.xq')   
        return
            replace(replace(replace($raw, '^file:/+', ''), '^.:', ''), '\\', '/')    
    let $dirInfo := replace(replace(replace(replace($dir, '^file:/+', ''), '^.:', ''), '\\', '/'), '/$', '')
    
    return (
        f:deploy($dir, $mod, $moduleText),
        
        let $requestBuild := tt:setParam((), 'dir', $dir, 'directory')
        let $requestBuild:= tt:setParam($requestBuild, 'name', $name, 'xs:NCName')        
        let $requestBuild:= tt:setParam($requestBuild, 'upgrade', 'false', 'xs:boolean')        
        return       
            f:build($requestBuild)
        ,
        <_>        
===============================================================
XQuery module created: {$moduleFname}
Operations:            {$ops}

Directory:             {$dirInfo}
Topic tool:            {$name}
           
The new operations are available. Example:

   basex -b "request={$ops[1]}?doc=doc1.xml doc2.xml doc3.xml" {$toolUri}
   
To implement them, edit these functions: {$ops}    
===============================================================
        </_>/text()
    )        
};

(:~
 : Writes the prototype of an application module.
 :)
declare function f:writeModule($ttname as xs:string,
                               $toolNamespace as xs:string,
                               $moduleName as xs:string, 
                               $ops as xs:string+, 
                               $features as xs:string*)
        as xs:string {
    let $toolStructureNamespace :=
        if (ends-with($toolNamespace, '/xquery-functions')) then 
            replace($toolNamespace, '/xquery-functions$', '/structure')
        else concat($toolNamespace, '/structure')            
    let $rawText :=  
    
    <TEXT>        
(:
 : -------------------------------------------------------------------------
 :
 : {$moduleName}.mod.xq - Document me!
 :
 : -------------------------------------------------------------------------
 :)
 
(:~@operations
   &lt;operations>{
      for $op in $ops return
      <NESTED-TEXT>
      &lt;operation name="{$op}" type="node()" func="{$op}">     
         &lt;param name="doc" type="docURI*" sep="WS" pgroup="input"/>
(:#file#:)         
         &lt;param name="docs" type="docDFD*" sep=";" pgroup="input"/>
(:##:)         
         &lt;param name="dcat" type="docCAT*" sep="WS" pgroup="input"/>
         &lt;pgroup name="input" minOccurs="1"/>         
      &lt;/operation>
      </NESTED-TEXT>/replace(., '\s+$', '', 's') }
    &lt;/operations>  
:)  

module namespace f="{$toolNamespace}";
import module namespace tt="http://www.ttools.org/xquery-functions" at 
    "_request.mod.xq",
    "_reportAssistent.mod.xq",
    "_errorAssistent.mod.xq",    
    "_nameFilter.mod.xq";
    
declare namespace z="{$toolStructureNamespace}";
{
for $op in $ops return
<NESTED-TEXT>
(:~
 : Document me!
 :
 : @param request the operation request
 : @return a report describing ...
 :) 
declare function f:{$op}($request as element())
        as element() {{
(:#file#:)        
    let $docs := tt:getParams($request, 'doc docs dcat')    
(:#file-#:)        
    let $docs := tt:getParams($request, 'doc dcat')
(:##:)
    return
        &lt;z:{$op} countDocs="{{count($docs)}}">{{
           ()
        }}&lt;/z:{$op}>
}};        
</NESTED-TEXT>/string()
}

</TEXT>/replace(., '^\s+', '', 's')   

    return
        f:filterTextByFeatures($rawText, $features)
};    

(:~
 : Deploys the topic tool and its extensions module.
 :)
declare function f:deploy($dir as xs:string, 
                          $moduleName as xs:string,
                          $moduleText as xs:string)
        as element(z:error)? {

    let $useDir := trace( replace(replace(resolve-uri($dir, static-base-uri()), '\\', '/'), '([^/])$', '$1/') , 'USEDIR: ')
    let $moduleURI := string-join(($useDir, concat($moduleName, '.mod.xq')), '')            
    return   
        if (file:exists($moduleURI)) then
                <z:error  
                     type="INVALID_PARAMETER_TYPE" subType="TYPE_ERROR" paramName="module" itemType="xs:NCName" 
                     itemValue="{$moduleName}" facet="fileExists" facetValue="false" 
                     msg="{concat('Parameter ''', 'module', ''': file ', $moduleURI, ''' already exists.')}"/>
        else        
            file:write($moduleURI, $moduleText, $f:serParamsText)        
};
