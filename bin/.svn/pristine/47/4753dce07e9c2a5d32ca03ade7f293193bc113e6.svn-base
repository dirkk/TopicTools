    (: builder.mod.xq - builds a topic tool
 :
 : @version 20140628-1 
 : ===================================================================================
 :)

(:~@operations
   <operations>
        <operation name="new" func="new" type="empty-sequence()">
            <param name="dir" type="directory" dirExists="false"/>
            <param name="name" type="xs:NCName"/>
            <param name="explain" type="xs:string?"/>           
            <param name="module" type="xs:NCName?"/>
            <param name="ops" type="xs:NCName*"/>            
            <param name="flavor" type="xs:string*" fct_values="xq10, xq10f, xq30, xq30f" default="xq30f"/>            
        </operation>
        <operation name="build" func="new" type="empty-sequence()">
            <param name="dir" type="directory" dirExists="false"/>
            <param name="name" type="xs:NCName"/>
            <param name="explain" type="xs:string?"/>
            <param name="upgrade" type="xs:boolean" default="true"/>       
            <param name="flavor" type="xs:string*" fct_values="xq10, xq10f, xq30, xq30f" default="xq30+"/>           
        </operation>
    </operations>   
:)


module namespace f="http://www.ttools.org/ttools/xquery-functions";

import module namespace tt="http://www.ttools.org/xquery-functions" at
    "_constants.mod.xq",
    "_request.mod.xq",
    "_request_setters.mod.xq",    
    "_reportAssistent.mod.xq",
    "_nameFilter.mod.xq";
    
import module namespace i="http://www.ttools.org/ttools/xquery-functions" at
    "builder_main.mod.xq",
    "builder_extensions.mod.xq",
    "toolSchemeParser.mod.xq",
    "util.mod.xq";
    
declare namespace z="http://www.ttools.org/ttools/structure";
declare namespace ztt="http://www.ttools.org/structure";
declare namespace soap="http://schemas.xmlsoap.org/soap/envelope/";

declare variable $f:cfg :=
    <cfg>
        <install>
            <modules>
                <module>_constants.mod.xq</module>               
                <module>_dcat.mod.xq</module>
                <module>_docs.mod.xq</module>                
                <module>_help.mod.xq</module>                
                <module>_nameFilter.mod.xq</module>            
                <module>_nameFilter_parser.mod.xq</module>                
                <module>_namespaceTools.mod.xq</module>
                <module>_reportAssistent.mod.xq</module>                
                <module>_errorAssistent.mod.xq</module>                
                <module>_request.mod.xq</module>
                <module>_request_getters.mod.xq</module>                
                <module>_request_parser.mod.xq</module> 
                <module>_request_setters.mod.xq</module>
                <module>_valueValidator.mod.xq</module>
                <module>_stringTools.mod.xq</module>                
                <module>_csvParser.mod.xq</module>                
            </modules>
            <examples>

            </examples>
        </install>
    </cfg>;

(:~
 : Creates a new topic tool.
 :
 : @param request the operation request
 : @return no return value, side effect is the installation / an update
 :    of the topic tool application.
 :) 
declare function f:new($request as element())
        as node(){
    let $dir as xs:string := tt:getParam($request, 'dir')
    let $nameParam := tt:getParam($request, 'name')
    let $toolName as xs:NCName := i:getToolName($dir, $nameParam)                
    let $mod as xs:NCName? := tt:getParam($request, 'mod')
    let $ops as xs:string* := tt:getParam($request, 'ops')
    let $flavor as xs:string := i:getToolFlavor($request, ())
    let $namespace as xs:string := i:getToolNamespace($request, $toolName, ())
    let $features := f:featuresFromFlavor($flavor)
    
    let $requestBuild := tt:setParam($request, 'upgrade', true(), 'xs:boolean')
    let $requestBuild := tt:setParam($requestBuild, 'namespace', $namespace, 'xs:string')    
    let $ttoolsUri := replace(replace(static-base-uri(), '^file:/+(.:)?', ''), '^(.*)/.*', '$1/ttools.xq')
    
    let $dirInfo := replace(replace(replace(replace($dir, '^file:/+', ''), '^.:', ''), '\\', '/'), '/$', '')
    let $toolUri := concat($dirInfo, '/', $toolName, '.xq')
    let $toolIdent :=
        if (ends-with($dirInfo, concat('/', $toolName))) then concat('dir=', $dirInfo)
        else concat('name=', $toolName, ', dir=', $dirInfo)     
    let $result := (
        file:create-dir($dir),

        let $result_build := f:build($requestBuild) 
        return
            if ($result_build/self::ztt:errors) then $result_build
            else if ($mod and exists($ops)) then (
                let $requestExtend:= tt:setParam($requestBuild, 'upgrade', false(), 'xs:boolean')        
                let $moduleText := i:writeModule($toolName, $namespace, $mod, $ops, $features)
                return (
                    f:deploy($dir, $mod, $moduleText),
                    f:build($requestExtend)
                )
            ) else (),
        
        <_>        
===============================================================

Topic tool created:  {$toolName}
Tool directory:      {$dirInfo}

The tool can already be called. Example:
   basex -b "request=?" {$toolUri}

Use operation 'add' for adding module prototypes. Example:
   basex -b "request=add?{$toolIdent}, mod=fooMod, ops=fooOp barOp foobarOp" {$ttoolsUri}  
===============================================================
        </_>/text()
    )
    let $errors := $result[. instance of element(ztt:errors)]
    return
        if ($errors) then $errors else $result
        
};

(:~
 : Rebuilds a topic tool.
 :
 : @params request request message
 : @return no return value, but contents of tool directory have been updated
 :)
declare function f:build($request as element())
        as element()? {       
    let $dir as xs:string := tt:getParam($request, 'dir')    
    let $useDir := resolve-uri($dir, static-base-uri())   
    let $toolName as xs:NCName := 
        let $nameParam as xs:NCName? := tt:getParam($request, 'name')
        return i:getToolName($dir, $nameParam)    
    let $settings := i:getToolSettings($dir, $toolName)
    let $flavor as xs:string := i:getToolFlavor($request, $settings)
    let $namespace as xs:string := i:getToolNamespace($request, $toolName, $settings)
    let $upgrade as xs:boolean := tt:getParam($request, 'upgrade')    
    let $explain as xs:string? := tt:getParam($request, 'explain')
    let $features := f:featuresFromFlavor($flavor)
    return (
        if (not($upgrade)) then () else f:copyFramework($dir, $toolName, $features),
        let $toolScheme := i:getToolScheme($dir, $toolName, $features)    
        return
            if ($toolScheme/self::ztt:errors) then $toolScheme else (
                (: generate & deploy the topic tool entry module and the extensions module :)
                let $topicTool := f:makeMainModule($toolScheme, $explain, $namespace)
                let $extensionsModule := f:makeExtensionsModule($toolScheme, $namespace)    
                return
                    f:deploy($dir, $toolName, $namespace, $flavor, $topicTool, $extensionsModule)
            )                    
       )
};

(:~
 : Checks the application directory.
 :)
declare function f:checkDir($dir as xs:string, $install as xs:boolean)
        as xs:string {
    let $useDir := 
        let $staticBaseUri := static-base-uri()
        return    
            resolve-uri($dir, $staticBaseUri) 
    return
        if (not($install or file:exists($useDir))) then
            let $dispDir := replace($useDir, '^file:/', '') return
                error(QName((), 'INVALID_ARG'), concat('Application directory ',
                $useDir, ' does not exist, and install flag is not set.&#xA;',
                '              Use install flag (install) if you wish to create a ',
                'new topic tool application;&#xA;',
                '              otherwise, correct the, $dir argument.'))
        else
            $useDir
};    

(:~
 : Installs an xquery topic tool application.
 : (a) creates the directory, if not already existing
 : (b) copies files into the directory (modules and examples)
 :
 : @param dir the application directory
 : @param toolName the topic tool name
 : @param features the features of the current tool flavor 
 : @return empty sequence
 :)
declare function f:copyFramework($dir as xs:string, $toolName as xs:string, $features as xs:string*)
        as empty-sequence() {       
    let $useDir := resolve-uri($dir, static-base-uri()) return (

    if (file:exists($useDir)) then () else file:create-dir($useDir)
    ,       
    for $module in $f:cfg//modules/module
    let $rawText := unparsed-text($module)
    let $text := f:filterTextByFeatures($rawText, $features)
    let $target := string-join(($useDir, $module), '/')
    return
        file:write($target, $text, $i:serParamsText)
    ,        
    for $module in $f:cfg//examples/example
    let $rawText := unparsed-text($module)
    let $text := f:filterTextByFeatures($rawText, $features)
    let $text := replace($text, 'www.ttools.org/ttools/', concat('www.ttools.org/', $toolName, '/')) 
    let $target := string-join(($useDir, $module), '/')
    return
        file:write($target, $text, $i:serParamsText)
    )        
};

(:~
 : Deploys the topic tool and its extensions module.
 : 
 : @param dir the tool directory
 : @param toolName the tool name
 : @param namespace of the functions implementing the tool operations
 : @param flavor the tool flavor
 : @param topicTool text of the tool main module
 : @param extensionsModule text of the extensions module, which is the module
 :    calling application-provided framework extensions
 : @return empty sequence
 :)
declare function f:deploy($dir as xs:string, 
                          $toolName as xs:string, 
                          $namespace as xs:string,
                          $flavor as xs:string,                          
                          $topicTool as xs:string, 
                          $extensionsModule as xs:string)
        as empty-sequence() {

    let $useDir := resolve-uri($dir, static-base-uri())
    return (
        let $topicToolURI := string-join(($useDir, concat($toolName, '.xq')), '/')            
        return            
            file:write($topicToolURI, $topicTool, $i:serParamsText),
        let $extensionsURI := string-join(($useDir, '_extensions.mod.xq'), '/')            
        return            
            file:write($extensionsURI, $extensionsModule, $i:serParamsText),
        let $settingsURI := concat($useDir, '/', $toolName, '-settings.xml')            
        let $newSettings :=
            <toolSettings toolName="{$toolName}">{
                <namespace>{$namespace}</namespace>,
                <flavor>{$flavor}</flavor>
            }</toolSettings>
        return
            file:write($settingsURI, $newSettings, $i:serParamsXml)
    )            
};

        